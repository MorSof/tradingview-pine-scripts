//@version=5
strategy("DR Soferian Strategy", overlay=true, default_qty_type=strategy.cash)

// === INPUTS ===
lookback_days_default = 10
benchmark_symbol = "SPX"
lookback_period = input.int(252, title="RS Lookback Period (Days)", minval=1)
ma50_len = input(50, title="MA 50 Length")
ma150_len = input(150, title="MA 150 Length")
ma200_len = input(200, title="MA 200 Length")
atr_len = input(lookback_days_default, title="ATR Length")
atr_mult = input(1.5, title="ATR Stop Multiplier")
trail_atr_mult = input(2.5, title="Trailing Stop ATR Multiplier (Base)")
vol_mult = input(1.6, title="Volume Breakout Multiplier")
weak_vol_mult = input(1.2, title="Weak Volume Multiplier")
breakout_lookback = input(lookback_days_default, title="Base Breakout Lookback")
avg_vol_lookback = input(lookback_days_default, title="Average Volume Lookback")
close_to_ma_pct = input(5.0, title="Buffer % Near 50MA Before Exiting")
extra_ma_buffer_pct = input(0.5, title="Extra Stop Buffer % Below 50MA")
account_size = input(60000, title="Account Size ($)")
use_ema21_exit = input.bool(true, title="Use 21 EMA as Exit")
consolidation_x = input.int(4, title="Min Pink Dots (X)", minval=1)
consolidation_y = input.int(lookback_days_default, title="Lookback Period for Pink Dots (Y)", minval=1)
base_min_bars = input.int(lookback_days_default, title="Minimum Base Duration (Bars)", minval=1)
max_dist_from_ema21_pct = input.float(15.0, title="Max Distance from EMA21 (%) at Entry", minval=0.1)

// === MOVING AVERAGES ===
ma50 = ta.sma(close, ma50_len)
ma150 = ta.sma(close, ma150_len)
ma200 = ta.sma(close, ma200_len)
ema21 = ta.ema(close, 21)

// === ATR & VOLUME ===
atr = ta.atr(atr_len)
highest_high = ta.highest(high, breakout_lookback)
avg_vol = ta.sma(volume, avg_vol_lookback)

// === VOLATILITY CONTRACTION ===
range_narrowing = atr == ta.lowest(atr, lookback_days_default)
plotshape(range_narrowing, style=shape.circle, location=location.abovebar, size=size.tiny, color=color.new(color.fuchsia, 0), title="Volatility Contraction")
pink_dot = range_narrowing ? 1 : 0
pink_dot_sum = math.sum(pink_dot, consolidation_y)
has_consolidated = pink_dot_sum >= consolidation_x

// === BASE DURATION CHECK (VCP-style) ===
base_high = ta.highest(close, base_min_bars)
base_low = ta.lowest(close, base_min_bars)
base_range_pct = 100 * (base_high - base_low) / base_low
tight_base = base_range_pct < 20
valid_base = tight_base and (bar_index - ta.lowestbars(low, base_min_bars) >= base_min_bars)

// === TREND CONDITIONS ===
trend_ok = close > ma50 and close > ma150 and close > ma200 and ma50 > ma150 and ma150 > ma200 and ma200 > ma200[20]

// === STRICTER BREAKOUT ===
close_near_high = close >= high - atr * 0.2
high_vol = volume > (avg_vol * vol_mult)
breakout_confirmed = close > highest_high[1] and close > open and close_near_high and high_vol

// === RS CALCULATION ===
benchmark = request.security(benchmark_symbol, "D", close)
stock_performance = close / close[lookback_period]
benchmark_performance = benchmark / benchmark[lookback_period]
is_rs_strong = stock_performance > benchmark_performance

// === RS NEW HIGH ===
rs_ratio = stock_performance / benchmark_performance
rs_new_high = rs_ratio > ta.highest(rs_ratio, 252)[1]

// === RS STATUS TABLE ===
var table rs_status_table = table.new(position.top_right, 1, 2, border_width=1)
rs_text = is_rs_strong ? "RS > SPX" : "RS <= SPX"
rs_color = is_rs_strong ? color.green : color.red
rsnh_text = rs_new_high ? "RS NH" : "RS not NH"
rsnh_color = rs_new_high ? color.green : color.gray
table.cell(rs_status_table, 0, 0, text=rs_text, bgcolor=rs_color, text_color=color.white)
table.cell(rs_status_table, 0, 1, text=rsnh_text, bgcolor=rsnh_color, text_color=color.white)

// === S&P 500 200MA FILTER ===
benchmark_ma = request.security(benchmark_symbol, "D", ta.sma(close, 150))
spx_trending_up = benchmark > benchmark_ma

// === 52-WEEK HIGH ===
week52_high = ta.highest(high, 252)
plot(week52_high, title="52-Week High", color=color.new(#FF6347, 0), style=plot.style_line, linewidth=3)
near_52w_high = close >= week52_high * 0.85

// === DISTANCE TO EMA21 ===
ema21_dist_pct = 100 * math.abs(close - ema21) / ema21
near_ema21 = ema21_dist_pct <= max_dist_from_ema21_pct

// === POSITION SIZE: FULL ACCOUNT ===
pos_size = account_size / close

// === ENTRY CONDITIONS ===
is_recent = (year(time) >= (year(timenow) - 3))
watch = breakout_confirmed and not trend_ok
weak = breakout_confirmed and trend_ok and (volume > (avg_vol * weak_vol_mult)) and not (is_rs_strong and has_consolidated and valid_base and near_52w_high and near_ema21)
strong = trend_ok and breakout_confirmed and is_rs_strong and has_consolidated and valid_base and near_52w_high and spx_trending_up and near_ema21 and is_recent
super_strong = strong and close > week52_high[1] and rs_new_high and base_range_pct < 15

if (strong)
    strategy.entry("Long", strategy.long, qty=pos_size)

// === R LEVELS ===
var float r1 = na
var float r2 = na
var float r3 = na
if (strategy.position_size > 0)
    entry_price = strategy.opentrades.entry_price(0)
    stop_dist = atr * atr_mult
    r1 := entry_price + stop_dist * 1
    r2 := entry_price + stop_dist * 2
    r3 := entry_price + stop_dist * 3
else
    r1 := na
    r2 := na
    r3 := na

// === TRACK R LEVELS HIT ===
var bool r1_hit = false
var bool r2_hit = false
var bool r3_hit = false
if (strategy.position_size > 0)
    if (not r1_hit and high >= r1)
        strategy.close("Long", qty_percent=25, comment="Close long partially at R1")
        r1_hit := true
    if (not r2_hit and high >= r2)
        strategy.close("Long", qty_percent=25, comment="Close long partially at R2")
        r2_hit := true
    if (not r3_hit and high >= r3)
        strategy.close("Long", qty_percent=25, comment="Close long partially at R3")
        r3_hit := true

// === EXIT LOGIC ===
var float trail = na
if (strategy.position_size > 0)
    high_in_trade = ta.highest(high, bar_index - strategy.opentrades.entry_bar_index(0) + 1)
    gain = (high_in_trade - strategy.opentrades.entry_price(0)) / strategy.opentrades.entry_price(0)
    trail_mult = gain > 0.3 ? trail_atr_mult * 2.0 : gain > 0.15 ? trail_atr_mult * 1.5 : trail_atr_mult
    trail := high_in_trade - (atr * trail_mult)
else
    trail := na

var float smart_stop = na
if (strategy.position_size > 0)
    dist_to_50 = trail - ma50
    buffer = close_to_ma_pct / 100.0 * close
    use_50 = (dist_to_50 <= buffer) and (ma50 > 0)
    stop_at = use_50 ? (ma50 * (1 - extra_ma_buffer_pct / 100.0)) : trail
    smart_stop := stop_at
else
    smart_stop := na

ema21_exit = use_ema21_exit and strategy.position_size > 0 and close < ema21
stop_hit = strategy.position_size > 0 and (low <= smart_stop)
if (ema21_exit or stop_hit)
    strategy.close("Long", comment=ema21_exit ? "Close on 21 EMA" : "Close on Trail")

// === PLOTS ===
plot(r1, title="R1", color=color.new(color.gray, 0), style=plot.style_linebr, linewidth=1)
plot(r2, title="R2", color=color.new(color.gray, 0), style=plot.style_linebr, linewidth=1)
plot(r3, title="R3", color=color.new(color.gray, 0), style=plot.style_linebr, linewidth=1)
plotshape(watch, style=shape.circle, location=location.abovebar, size=size.tiny, color=color.new(color.gray, 0), title="Watch", text="Watch")
plotshape(weak, style=shape.diamond, location=location.abovebar, size=size.small, color=color.new(color.orange, 0), title="Weak", text="Weak")
plotshape(strong, style=shape.triangleup, location=location.belowbar, size=size.normal, color=color.new(color.lime, 0), title="Strong", text="Strong")
plotshape(super_strong, style=shape.triangleup, location=location.belowbar, size=size.large, color=color.new(color.aqua, 0), title="Super Strong", text="Super")
plot(ma50, color=color.new(color.green, 0), title="MA50", linewidth=2)
plot(ma150, color=color.new(color.blue, 0), title="MA150", linewidth=2)
plot(ma200, color=color.new(color.red, 0), title="MA200", linewidth=2)
plot(ema21, color=color.new(#00FFFF, 0), title="EMA21", linewidth=2)
plot(smart_stop, color=color.purple, title="TrailStop", style=plot.style_linebr, linewidth=2)

// === ALERTS ===
alertcondition(watch, title="Watch Signal", message="Watch level breakout.")
alertcondition(weak, title="Weak Signal", message="Weak breakout attempt.")
alertcondition(strong, title="Strong Signal", message="Strong breakout setup.")
alertcondition(super_strong, title="Super Strong Signal", message="Super strong breakout with strict confirmation.")
