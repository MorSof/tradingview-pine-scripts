//@version=5
indicator("DR Soferian Screener", overlay=true)

// === INPUTS ===
lookback_days_default = 10
benchmark_symbol = "SPX"
lookback_period = input.int(252, title="RS Lookback Period (Days)", minval=1)
ma50_len = input(50, title="MA 50 Length")
ma150_len = input(150, title="MA 150 Length")
ma200_len = input(200, title="MA 200 Length")
atr_len = input(lookback_days_default, title="ATR Length")
atr_mult = input(1.5, title="ATR Stop Multiplier")
trail_atr_mult = input(2.5, title="Trailing Stop ATR Multiplier (Base)")
vol_mult = input(1.6, title="Volume Breakout Multiplier")
weak_vol_mult = input(1.2, title="Weak Volume Multiplier")
breakout_lookback = input(lookback_days_default, title="Base Breakout Lookback")
avg_vol_lookback = input(lookback_days_default, title="Average Volume Lookback")
close_to_ma_pct = input(5.0, title="Buffer % Near 50MA Before Exiting")
extra_ma_buffer_pct = input(0.5, title="Extra Stop Buffer % Below 50MA")
use_ema21_exit = input.bool(true, title="Use 21 EMA as Exit")
consolidation_x = input.int(4, title="Min Pink Dots (X)", minval=1)
consolidation_y = input.int(lookback_days_default, title="Lookback Period for Pink Dots (Y)", minval=1)
base_min_bars = input.int(lookback_days_default, title="Minimum Base Duration (Bars)", minval=1)
max_dist_from_ema21_pct = input.float(15.0, title="Max Distance from EMA21 (%) at Entry", minval=0.1)
fallback_base_width_pct = input.float(10.0, title="Max Base Width % (Fallback)", minval=1.0)
tight_base_lookback = input.int(5, title="Lookback Period for Green Dots (Tight Base)", minval=1)
min_tight_base_dots = input.int(4, title="Min Green Dots (Tight Base)", minval=1)

// === INDICATOR LOGIC ===
// MAs
ma50 = ta.sma(close, ma50_len)
ma150 = ta.sma(close, ma150_len)
ma200 = ta.sma(close, ma200_len)
ema21 = ta.ema(close, 21)

// ATR & Volume
atr = ta.atr(atr_len)
highest_high = ta.highest(high, breakout_lookback)
avg_vol = ta.sma(volume, avg_vol_lookback)

// ATR Percentile for contraction
atr_min = ta.lowest(atr, consolidation_y)
atr_max = ta.highest(atr, consolidation_y)
atr_percentile = 100 * (atr - atr_min) / (atr_max - atr_min)
range_narrowing = atr_percentile < 20
plotshape(range_narrowing, style=shape.circle, location=location.abovebar, size=size.tiny, color=color.new(color.fuchsia, 0), title="Volatility Contraction (ATR%)")
pink_dot = range_narrowing ? 1 : 0
pink_dot_sum = math.sum(pink_dot, consolidation_y)
has_consolidated = pink_dot_sum >= consolidation_x

// Base validity
base_high = ta.highest(close, base_min_bars)
base_low = ta.lowest(close, base_min_bars)
base_range_pct = 100 * (base_high - base_low) / base_low
atr_start = ta.linreg(atr, base_min_bars, 0)
atr_end = ta.linreg(atr, base_min_bars, base_min_bars - 1)
atr_slope = (atr_start - atr_end) / base_min_bars
atr_decreasing = atr_slope < 0
tight_base = (base_range_pct < fallback_base_width_pct) and atr_decreasing
valid_base = tight_base and (bar_index - ta.lowestbars(low, base_min_bars) >= base_min_bars)
plotshape(valid_base == 1, title="Tight Contraction Dot", location=location.belowbar, style=shape.circle, size=size.tiny, color=color.green, offset=0)

green_dot = valid_base ? 1 : 0
green_dot_sum = math.sum(green_dot, tight_base_lookback)
has_tight_base_recently = green_dot_sum >= min_tight_base_dots

// Trend
trend_ok = close > ma50 and close > ma150 and close > ma200 and ma50 > ma150 and ma150 > ma200 and ma200 > ma200[20]

// Breakout
close_near_high = close >= high - atr * 0.2
high_vol = volume > (avg_vol * vol_mult)
breakout_confirmed = close > highest_high[1] and close > open and close_near_high and high_vol

// RS
benchmark = request.security(benchmark_symbol, "D", close)
stock_perf = close / close[lookback_period]
bench_perf = benchmark / benchmark[lookback_period]
is_rs_strong = stock_perf > bench_perf
rs_ratio = stock_perf / bench_perf
rs_new_high = rs_ratio > ta.highest(rs_ratio, 252)[1]

// S&P filter
benchmark_ma = request.security(benchmark_symbol, "D", ta.sma(close, 150))
spx_trending_up = benchmark > benchmark_ma

// 52w high
week52_high = ta.highest(high, 252)
near_52w_high = close >= week52_high * 0.85

// EMA21 distance
ema21_dist_pct = 100 * math.abs(close - ema21) / ema21
near_ema21 = ema21_dist_pct <= max_dist_from_ema21_pct

// Time filter
is_recent = (year(time) >= (year(timenow) - 3))

// Signals
watch = breakout_confirmed and not trend_ok
weak = breakout_confirmed and trend_ok and (volume > (avg_vol * weak_vol_mult)) and not (is_rs_strong and has_consolidated and has_tight_base_recently and near_52w_high and near_ema21)
strong = trend_ok and breakout_confirmed and is_rs_strong and has_consolidated and has_tight_base_recently and near_52w_high and spx_trending_up and near_ema21 and is_recent
super_strong = strong and close > week52_high[1] and rs_new_high

// === Plots for Screener ===
plotshape(watch, title="Watch", style=shape.circle, location=location.abovebar, color=color.gray, size=size.small)
plotshape(weak, title="Weak", style=shape.diamond, location=location.abovebar, color=color.orange, size=size.small)
plotshape(strong, title="Strong", style=shape.triangleup, location=location.belowbar, color=color.lime, size=size.normal)
plotshape(super_strong, title="Super Strong", style=shape.triangleup, location=location.belowbar, color=color.aqua, size=size.large)
plot(ma50, color=color.new(color.green, 0), title="MA50", linewidth=2)
plot(ma150, color=color.new(color.blue, 0), title="MA150", linewidth=2)
plot(ma200, color=color.new(color.red, 0), title="MA200", linewidth=2)
plot(ema21, color=color.new(#00FFFF, 0), title="EMA21", linewidth=2)

// === Alert Conditions ===
alertcondition(watch, title="Watch Alert", message="Watch setup detected.")
alertcondition(weak, title="Weak Alert", message="Weak breakout setup.")
alertcondition(strong, title="Strong Alert", message="Strong breakout setup.")
alertcondition(super_strong, title="Super Strong Alert", message="Super strong breakout setup.")
